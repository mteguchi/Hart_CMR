# Cormack-Jolly-Seber model - p.179 on Kery and 
# Schaub, with individual- and time-specific covariate on survival

model {
  for (i in 1:nind){
     for (t in f[i]:(n.occasions-1)){
        logit(phi[i, t]) <- mu + beta * x[i, t] + epsilon[i]
     }
     epsilon[i] ~ dnorm(0, tau)
  }

  for (t in 1:T){
    p[t] ~ dbeta(5, 2)
    N[t] <- n[t]/p[t]
  }

  mean.phi ~ dbeta(5, 2) #unif(0,1)
  mu <- log(mean.phi / (1 - mean.phi))

  beta ~ dnorm(0, 0.01)
  sigma ~ dunif(0, 3)
  tau <- pow(sigma, -2)
  
  # Likelihood:
  for (i in 1:nind){
    # define latent state at first capture
    z[i, f[i]] <- 1
    for (t in (f[i] + 1):n.occasions){
      # state process
      z[i,t] ~ dbern(mu1[i,t])
      mu1[i,t] <- (phi[i, t-1] ^ dt[t]) * z[i, t-1]

      # observation process
      y[i,t] ~ dbern(mu2[i,t])
      mu2[i,t] <- p[t-1] * z[i,t]  
    }
  }

}


